<?xml version="1.0" encoding="utf-8"?>
<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform">
<xsl:output method="xml" indent="yes" />
<!-- external parameters -->
<xsl:param name="version" />
<xsl:param name="version.index" />
<xsl:template match="/application">
<manifest xmlns:android="http://schemas.android.com/apk/res/android"
      package="app.zxtune"
      android:versionCode="{$version.index}"
      android:versionName="{$version}"
      android:installLocation="auto" >
    <uses-sdk android:minSdkVersion="10" android:targetSdkVersion="17" />
    <!-- substitute permission -->
    <xsl:for-each select="permissions/android">
      <uses-permission android:name="android.permission.{text()}" />
    </xsl:for-each>
    
    <supports-screens android:largeScreens="true" android:xlargeScreens="true" />
    <application
          android:label="@string/app_name"
          android:icon="@drawable/ic_launcher"
          android:allowBackup="true"
          android:theme="@style/Theme.AppCompat"
        >
        <activity
              android:name=".MainActivity"
              android:label="@string/app_name">
            <intent-filter>
                <action android:name="android.intent.action.MAIN" />
                <category android:name="android.intent.category.LAUNCHER" />
            </intent-filter>
            <intent-filter>
                <action android:name="android.intent.action.VIEW" />
                <category android:name="android.intent.category.DEFAULT" />
                <category android:name="android.intent.category.BROWSABLE" />
                <!-- substitute file filters -->
                <xsl:for-each select="filter">
                  <xsl:for-each select="*">
                    <xsl:call-template name="filter">
                      <xsl:with-param name="scheme" select="name()"/>
                      <xsl:with-param name="types" select="text()"/>
                    </xsl:call-template>
                    <xsl:if test="@capital">
                      <xsl:call-template name="filter">
                        <xsl:with-param name="scheme" select="name()"/>
                        <xsl:with-param name="types" select="translate(text(), 'abcdefghijklmnopqrstuvwxyz', 'ABCDEFGHIJKLMNOPQRSTUVWXYZ')"/>
                      </xsl:call-template>
                    </xsl:if>
                  </xsl:for-each>
                </xsl:for-each>
            </intent-filter>
        </activity>
        <activity
              android:name=".PreferencesActivity"
              android:label="@string/prefs">
            <intent-filter>
                <action android:name="app.zxtune.PreferencesActivity" />
                <category android:name="android.intent.category.PREFERENCE" />
            </intent-filter>
        </activity>
        <service
              android:name=".MainService"
              android:process=":service">
        </service>
        <service
              android:name=".ScanService"
              android:process=":service">
        </service>
        <!--
          Providers are usually used only in UI.
          ICS does not support running provider in separate thread:
          https://groups.google.com/d/msg/android-developers/OGdHjmINxTA/qtYDlWq9TiUJ
        -->
        <provider
              android:name="app.zxtune.playlist.Provider"
              android:authorities="app.zxtune.playlist"
              android:exported="false">
        </provider>
        <!--
          Plugins enumerating provider to avoid loading zxtune library in UI part
        -->
        <provider
              android:name="app.zxtune.PluginsProvider"
              android:authorities="app.zxtune.plugins"
              android:process=":service"
              android:exported="false">
        </provider>
        <!--
          Static receiver should be used for remote controls
        -->
        <receiver android:name=".MediaButtonsHandler">
          <intent-filter>
            <action android:name="android.intent.action.MEDIA_BUTTON"/>
          </intent-filter>
        </receiver>
    </application>
</manifest>
</xsl:template>
<xsl:template name="filter" xmlns:android="http://schemas.android.com/apk/res/android">
  <xsl:param name="scheme"/>
  <xsl:param name="types"/>
  <xsl:for-each select="str:tokenize($types, ';,')" xmlns:str="http://exslt.org/strings">
    <data android:mimeType="*/*" android:scheme="{$scheme}" android:host="*" android:pathPattern=".*\\.{text()}" />
  </xsl:for-each>
</xsl:template>
</xsl:stylesheet>
